group 'org.platypus.tools'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1.1-SNAPSHOT'
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'me.champeau.gradle.antlr4'

compileJava.dependsOn antlr4
compileKotlin.dependsOn antlr4

sourceSets.main.java.srcDirs += antlr4.output
sourceSets.main.kotlin.srcDirs += antlr4.output

configurations {
    compile.extendsFrom antlr4
}

antlr4 {
    source = file('src/main/antlr')
    output = file('build/generated-src/org/platypus/modules/lang/kotlin')
    extraArgs = ['-package', 'org.platypus.modules.lang.kotlin']
}

repositories {
    mavenCentral()
    maven {
        url "https://dl.bintray.com/kotlin/exposed"
    }
}

clean{
    delete "build/generated-src"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile group: 'com.squareup', name: 'kotlinpoet', version: '0.4.0'
    compile 'org.jetbrains.exposed:exposed:0.8.2'
    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

idea {
    module {
        sourceDirs += file("build/generated-src/")
    }
}
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Kotlin-Parser',
                'Implementation-Version': '0.0.1'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}